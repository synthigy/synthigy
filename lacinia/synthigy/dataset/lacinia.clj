(ns synthigy.dataset.lacinia
  "GraphQL (Lacinia) schema generation for Synthigy dataset models.

  Copied from EYWA's working implementation."
  (:require
    [camel-snake-kebab.core :as csk]
    [clojure.string :as str]
    [clojure.tools.logging :as log]
    [com.walmartlabs.lacinia.executor :as executor]
    [com.walmartlabs.lacinia.parser :as parser]
    [com.walmartlabs.lacinia.resolve :as lacinia.resolve]
    [synthigy.dataset :as dataset]
    [synthigy.dataset.core :as core]
    [synthigy.dataset.id :as id]
    [synthigy.dataset.lacinia.enhance :as enhance]
    [synthigy.db :refer [*db*
                         sync-entity
                         stack-entity
                         slice-entity
                         get-entity
                         get-entity-tree
                         purge-entity
                         search-entity-tree
                         search-entity
                         aggregate-entity
                         aggregate-entity-tree
                         delete-entity]]
    [synthigy.lacinia]))

(def npattern #"[\s\_\-\.\$\[\]\{\}\#]+")

(def
  ^{:doc "Name normalization function"}
  normalize-name
  (memoize
    (fn
      [n]
      (clojure.string/lower-case
        (clojure.string/replace n npattern "_")))))

;;; ============================================================================
;;; ID Provider → GraphQL Type Mappings
;;; ============================================================================

(defn- id-scalar-type
  "Returns the GraphQL scalar type for ID field based on current provider.
   'UUID for UUIDProvider, 'String for NanoIDProvider."
  []
  (case (id/provider-type)
    :euuid 'UUID
    :xid 'String))

(defn- id-operator-type
  "Returns the GraphQL query operator type for ID field.
   :UUIDQueryOperator for UUIDProvider, :StringQueryOperator for NanoIDProvider."
  []
  (case (id/provider-type)
    :euuid :UUIDQueryOperator
    :xid :StringQueryOperator))

(defn- id-attr-type
  "Returns the internal attribute type string for ID field.
   \"uuid\" for UUIDProvider, \"string\" for NanoIDProvider."
  []
  (case (id/provider-type)
    :euuid "uuid"
    :xid "string"))

;;; ============================================================================
;;; Schema Generation
;;; ============================================================================

;; NOTE: Database operations (get-entity, search-entity, etc.) are protocol
;; methods defined in synthigy.db/ModelQueryProtocol and called on *db*.
;; The implementation is in synthigy.dataset.postgres.query via extend-type.

(defn scalar-attribute? [{t :type}]
  (contains?
    #{"int" "float" "boolean" "string" "avatar" "hashed"
      "encrypted" "timestamp" "timeperiod" "transit" "json" "uuid" "enum"}
    t))

(defn reference-attribute? [{t :type}]
  (contains? #{"user" "group" "role"} t))

(defn attribute->gql-field [n]
  (keyword (normalize-name n)))

(defn entity->gql-object [n]
  (csk/->PascalCaseKeyword n))

(defn entity->gql-input-object [n]
  (csk/->PascalCaseKeyword (str n " input")))

(defn entity-attribute->enum [{ename :name} {aname :name}]
  (csk/->PascalCaseKeyword (str ename \space aname)))

(defn entity-attribute->enum-operator [{ename :name} {aname :name}]
  (csk/->PascalCaseKeyword (str ename \space aname \space "operator")))

(def ignored-field-type? #{"transit" "json" "encrypted" "hashed"})

(defn entity->search-operator [{n :name}]
  (csk/->camelCaseKeyword (str "search " n " operator")))

(defn entity->order-by-operator [{n :name}]
  (csk/->camelCaseKeyword (str "order by " n " operator")))

(defn entity->attributes-enum [{n :name}]
  (csk/->camelCaseKeyword (str n "Attributes")))

(defn entity->distinct-on-operator [{n :name
                                     :as entity}]
  (when (nil? n)
    (println "ENTITY: " entity)
    (throw (Exception. "HII")))
  (csk/->camelCaseKeyword (str "distinct on " n " operator")))

; (defn entity->aggregate-object [{n :name}]
;   (csk/->PascalCaseKeyword (str n " aggregate")))

(defn entity->count-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " count")))

(defn entity->max-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " max")))

(defn entity->min-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " min")))

(defn entity->avg-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " avg")))

(defn entity->sum-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " sum")))

(defn entity->agg-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " aggregate")))

(defn entity->agg-part-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " agg")))

(defn entity->slice-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " slice")))

(defn attribute-type->scalar [entity {t :type
                                      :as attribute}]
  (case t
    "int" 'Int
    "float" 'Float
    "boolean" 'Boolean
    "avatar" 'String
    "string" 'String
    "encrypted" 'Encrypted
    "hashed" 'Hash
    "timestamp" 'Timestamp
    "timeperiod" 'TimePeriod
    "json" 'JSON
    "transit" 'Transit
    "uuid" 'UUID
    "enum" (entity-attribute->enum entity attribute)))

(defn attribute-type->operator [entity {t :type
                                        :as attribute}]
  (case t
    "int" {:type :IntegerQueryOperator}
    "float" {:type :FloatQueryOperator}
    "boolean" {:type :BooleanQueryOperator}
    "string" {:type :StringQueryOperator}
    "avatar" {:type :StringQueryOperator}
    "encrypted" {:type :StringQueryOperator}
    "timestamp" {:type :TimestampQueryOperator}
    "timeperiod" {:type :TimePeriodQueryOperator}
    "transit" {:type :StringQueryOperator}
    "uuid" {:type :UUIDQueryOperator}
    "enum" {:type (entity-attribute->enum-operator entity attribute)}
    {:type t}))

(defn numerics? [{as :attributes}]
  (filter
    (fn [{t :type}]
      (boolean (#{"int" "float"} t)))
    as))

(defn entity->numeric-object [{n :name}]
  (csk/->PascalCaseKeyword (str n " Numerics")))

(defn entity->relation-enum [{n :name}]
  (csk/->PascalCaseKeyword (str n " relations enum")))

(defn normalized-enum-value [value]
  (clojure.string/replace value #"-|\s" "_"))

(defn generate-lacinia-enums
  [model]
  (letfn [(model->enums []
            (let [entities (core/get-entities model)]
              (reduce
                (fn [enums {:keys [attributes]
                            :as entity}]
                  (as-> enums ens
                    ;; This may be required 
                    (assoc enums (entity->attributes-enum entity)
                           {:values (mapv
                                      (comp normalize-name :name)
                                      (filter scalar-attribute? attributes))})
                    (let [le (filter #(= (:type %) "enum") attributes)]
                      (if (not-empty le)
                        (reduce
                          (fn [enums {config :configuration
                                      :as attribute}]
                            (assoc enums (entity-attribute->enum entity attribute)
                                   (update config :values #(mapv (comp normalized-enum-value :name) %))))
                          ens
                          le)
                        ens))))
                {:BooleanCondition {:values ["TRUE" "FALSE" "NOT_TRUE" "NOT_FALSE" "NULL" "NOT_NULL"]}}
                entities)))]
    (merge
      (model->enums)
      {:SQLJoinType {:values [:LEFT :RIGTH :INNER]}
       :order_by_enum {:values [:asc :desc]}
       :is_null_enum {:values [:is_null :is_not_null]}})))

(defn generate-lacinia-objects
  [model]
  (letfn [(reference-object [id]
            (let [entity (core/get-entity model id)]
              {:type (entity->gql-object (:name entity))
               :args {:_where {:type (entity->search-operator entity)}
                      :_maybe {:type (entity->search-operator entity)}}}))
          (has-numerics? [{:keys [attributes]}]
            (some
              (fn [{t :type}]
                (#{"int" "float"} t))
              attributes))
          (get-numerics [{:keys [attributes]}]
            (reduce
              (fn [fields {atype :type
                           :as attribute}]
                (case atype
                  ("int" "float") (conj fields attribute)
                  fields))
              []
              attributes))]
    (let [entities (core/get-entities model)
          _who :modified_by
          _when :modified_on]
      (reduce
        (fn [r {ename :name
                attributes :attributes
                :as entity}]
          (if (empty? attributes) r
              (let [numerics (get-numerics entity)
                    scalars (filter scalar-attribute? attributes)
                    references (filter #(and (reference-attribute? %) (:active %)) attributes)
                    entity-relations (core/focus-entity-relations model entity)
                    to-relations (filter #(not-empty (:to-label %)) entity-relations)]
                (if (nil? ename) r
                    (cond->
                      (assoc
                        r (entity->gql-object ename)
                        {:fields (as->
                                   ;; Base fields - audit comes from protocol
                                   (merge
                                     {(id/key) {:type (id-scalar-type)}}
                                     ;; Get audit fields from enhancement protocol
                                     (enhance/get-audit-fields *db* entity))
                                   fields
                                   ;; References to well known objects
                                   (reduce
                                     (fn [fields {aname :name
                                                  t :type}]
                                       (case t
                                         "user" (assoc fields
                                                  (attribute->gql-field aname)
                                                  (reference-object (id/entity :iam/user)))
                                         "group" (assoc fields
                                                   (attribute->gql-field aname)
                                                   (reference-object (id/entity :iam/user-group)))
                                         "role" (assoc fields
                                                  (attribute->gql-field aname)
                                                  (reference-object (id/entity :iam/user-role)))))
                                     fields
                                     references)
                                   ;; Scalars
                                   (reduce
                                     (fn [fields {aname :name
                                                  atype :type
                                                  active :active
                                                  :as attribute}]
                                       (if-not (or active (= aname (id/field))) fields
                                               (let [t (attribute-type->scalar entity attribute)]
                                                 (assoc fields (attribute->gql-field aname)
                                                        (cond-> {:type t}
                                                          (= "enum" atype)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_eq :_neq :_in :_not_in]
                                                                    (concat
                                                                      (repeat 2 {:type t})
                                                                      (repeat 2 {:type (list 'list t)}))))
                                                          (= t 'UUID)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_eq :_neq :_in :_not_in]
                                                                    (concat
                                                                      (repeat 2 {:type 'UUID})
                                                                      (repeat 2 {:type (list 'list 'UUID)}))))
                                                          (= t 'Boolean)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_eq :_neq]
                                                                    (repeat {:type 'Boolean})))
                                                          (= t 'Int)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_gt :_lt :_eq :_neq :_ge :_le :_in :_not_in]
                                                                    (concat
                                                                      (repeat 6 {:type 'Int})
                                                                      (repeat 6 {:type (list 'list 'Int)}))))
                                                ;;
                                                          (= t 'Float)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_gt :_lt :_eq :_neq :_ge :_le :_in :_not_in]
                                                                    (concat
                                                                      (repeat 6 {:type 'Float})
                                                                      (repeat 6 {:type (list 'list 'Float)}))))
                                                          (= t 'String)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_neq :_eq :_like :_ilike :_in :_not_in]
                                                                    (concat
                                                                      (repeat 4 {:type 'String})
                                                                      (repeat 4 {:type (list 'list 'String)}))))
                                                          (= t 'Timestamp)
                                                          (assoc
                                                            :args (zipmap
                                                                    [:_gt :_lt :_neq :_eq :_ge :_le]
                                                                    (repeat {:type 'Timestamp})))
                                                          (= t 'Timeperiod)
                                                          (assoc
                                                            :args (merge
                                                                    (zipmap
                                                                      [:_gt :_lt :_neq :_eq :_ge :_le]
                                                                      (repeat {:type 'TimePeriod}))
                                                                    (zipmap
                                                                      [:contains :exclude]
                                                                      (repeat {:type 'Timestamp})))))))))
                                     fields
                                     (conj scalars
                                           {:name (id/field)
                                            :type (id-attr-type)}))
                               ;; Outgoing relations
                                   (reduce
                                     (fn [fields {:keys [to to-label cardinality]}]
                                       (if (and (not-empty to-label) (not-empty (:name to)))
                                         (assoc fields (attribute->gql-field to-label)
                                                (let [t (entity->gql-object (:name to))
                                                      to-search (entity->search-operator to)
                                                      tree-search (entity->search-operator entity)]
                                              ;; TODO - rethink _maybe and _where
                                              ;; It is essentially opening for _and _or and
                                              ;; Can it be skipped?
                                              ;; I think that it was just convinience to use operator
                                              ;; instead of generating more argument... That is more code
                                                  (case cardinality
                                                    ("o2m" "m2m") {:type (list 'list t)
                                                                   :args {:_offset {:type 'Int}
                                                                          :_limit {:type 'Int}
                                                                          :_where {:type to-search}
                                                                          :_join {:type :SQLJoinType}
                                                                          :_maybe {:type to-search}
                                                                          :_order_by {:type (entity->order-by-operator to)}}}
                                                    ("m2o" "o2o") {:type t
                                                                   :args {:_where {:type to-search}
                                                                          :_join {:type :SQLJoinType}
                                                                          :_maybe {:type to-search}}}
                                                    "tree" {:type t
                                                            :args {:_where {:type tree-search}
                                                                   :_join {:type :SQLJoinType}
                                                                   :_maybe {:type tree-search}
                                                                   (attribute->gql-field to-label) {:type :is_null_enum}}}
                                                    {:type t})))
                                         fields))
                                     fields
                                     to-relations)
                               ;; This was used when _agg wasn't defined
                                   (if (empty? to-relations) fields
                                       (cond->
                                         (assoc fields :_count {:type (entity->count-object entity)})
                                   ;; FUTURE Self - This doesn't make sense since other
                                   ;; entities will have _agg option. I can't think of use case
                                   ;; where this is relevant at target entity level
                                   ; (not-empty numerics)
                                   ; (assoc
                                   ;   :_max {:type (entity->numeric-object entity)}
                                   ;   :_min {:type (entity->numeric-object entity)}
                                   ;   :_avg {:type (entity->numeric-object entity)}
                                   ;   :_sum {:type (entity->numeric-object entity)})
                                   ;;
                                         (some has-numerics? (map :to to-relations))
                                         (assoc :_agg {:type (entity->agg-object entity)}))))})
                  ;;
                      (some has-numerics? (map :to to-relations))
                      (as-> objects
                            (reduce
                              (fn [objects {:keys [to]}]
                                (if-not (has-numerics? to) objects
                                        (assoc objects
                                          (entity->agg-part-object to)
                                          {:fields
                                           {:_max {:type (entity->numeric-object to)}
                                            :_min {:type (entity->numeric-object to)}
                                            :_avg {:type (entity->numeric-object to)}
                                            :_sum {:type (entity->numeric-object to)}}})))
                              objects
                              to-relations))
                  ;;
                      (not-empty to-relations)
                      (assoc
                    ;;
                        (entity->slice-object entity)
                        {:fields
                         (reduce
                           (fn [fields {:keys [to to-label]}]
                             (if (not-empty to-label)
                               (assoc fields (attribute->gql-field to-label)
                                      {:type 'Boolean
                                       :args {:_where {:type (entity->search-operator to)}}})
                               fields))
                           nil
                           entity-relations)}
                    ;;
                        (entity->count-object entity)
                        {:fields (reduce
                                   (fn [fields {:keys [to to-label]}]
                                     (if (not-empty to-label)
                                       (assoc fields (attribute->gql-field to-label)
                                              {:type 'Int
                                               :args {:_where {:type (entity->search-operator to)}}})
                                       fields))
                                   nil
                                   entity-relations)})
                  ;;
                      (not-empty numerics)
                      (assoc (entity->numeric-object entity)
                        {:fields (reduce
                                   (fn [result attribute]
                                     (assoc result (attribute->gql-field (:name attribute))
                                             ; {:type (attribute-type->scalar entity attribute)
                                            {:type 'Float
                                             :args {:_where {:type (entity->search-operator entity)}}}))
                                   nil
                                   numerics)})
                  ;;
                      true
                      (assoc (entity->agg-object entity)
                        {:fields
                         (cond->
                           (reduce
                             (fn [fields {:keys [to to-label]}]
                               (if (not-empty to-label)
                                 (assoc fields (attribute->gql-field to-label)
                                        {:type (entity->agg-object to)
                                         :args {:_where {:type (entity->search-operator to)}}})
                                 fields))
                             (reduce
                               (fn [fields {t :type
                                            n :name}]
                                 (assoc fields (attribute->gql-field n)
                                        (case t
                                          "int"
                                          {:type :IntAggregate
                                           :args (zipmap
                                                   [:_gt :_lt :_eq :_neq :_ge :_le]
                                                   (repeat {:type 'Int}))}
                                          "float"
                                          {:type :FloatAggregate
                                           :args (zipmap
                                                   [:_gt :_lt :_eq :_neq :_ge :_le]
                                                   (repeat {:type 'Float}))})))
                               {}
                               (numerics? entity))
                             to-relations))}))))))
        {:IntAggregate
         {:fields
          {:min {:type 'Int}
           :max {:type 'Int}
           :sum {:type 'Int}
           :avg {:type 'Float}}}
         :FloatAggregate
         {:fields
          {:min {:type 'Float}
           :max {:type 'Float}
           :sum {:type 'Float}
           :avg {:type 'Float}}}}
        entities))))

(defn generate-lacinia-input-objects
  [model]
  (let [entities (core/get-entities model)
        user (core/get-entity model (id/entity :iam/user))
        group (core/get-entity model (id/entity :iam/user-group))
        role (core/get-entity model (id/entity :iam/user-role))
        user-input (csk/->PascalCaseKeyword (str (:name user) " input"))
        group-input (csk/->PascalCaseKeyword (str (:name group) " input"))
        role-input (csk/->PascalCaseKeyword (str (:name role) " input"))]
    (letfn [(referenced-entity [{atype :type}]
              (case atype
                "user" user
                "group" group
                "role" role))]
      (reduce
        (fn [r {ename :name
                attributes :attributes
                :as entity}]
          (let [i (csk/->PascalCaseKeyword (str ename " input"))
                s (entity->search-operator entity)
                o (entity->order-by-operator entity)
                d (entity->distinct-on-operator entity)
                relations (core/focus-entity-relations model entity)
                recursions (filter
                             #(= "tree" (:cardinality %))
                             relations)
                attributes' (conj attributes
                                  {:name (id/field)
                                   :type (id-attr-type)
                                   :active true}
                                  {:name "modified_on"
                                   :type "timestamp"
                                   :active true})]
            (assoc
              (reduce
                (fn [r attribute]
                  (let [t (entity-attribute->enum entity attribute)]
                    (assoc r (entity-attribute->enum-operator entity attribute)
                           {:fields (zipmap
                                      [:_eq :_neq :_in :_not_in]
                                      (concat
                                        (repeat 2 {:type t})
                                        (repeat 2 {:type (list 'list t)})))})))
                r
                (filter #(= (:type %) "enum") attributes))
              ;; input fields
              i {:fields (as-> {} input
                           ;;
                           (reduce
                             (fn [fields {;constraint :constraint
                                          aname :name
                                          atype :type
                                          active :active
                                          :as attribute}]
                               (if-not (or active (= aname (id/field)))
                                 fields
                                 (assoc fields
                                   (keyword (normalize-name aname))
                                   {:type (let [t (case atype
                                                    "timeperiod"
                                                    'TimePeriodInput
                                                         ;;
                                                    "user"
                                                    user-input
                                                         ;;
                                                    "group"
                                                    group-input
                                                         ;;
                                                    "role"
                                                    role-input
                                                         ;;
                                                    (attribute-type->scalar entity attribute))]
                                            t
                                                 ;; TODO - To be fully compliant enable this
                                                 ;; For now this is disabled to enable easier
                                                 ;; data storing for eywa.dataset namespace
                                                 ;; that splits data into stack and slice mutations
                                            #_(if (= constraint "mandatory")
                                                (list 'non-null t)
                                                t))})))
                             input
                             (conj attributes
                                   {:name (id/field)
                                    :type (id-attr-type)}))
                           ;;
                           (reduce
                             (fn [fields {:keys [to to-label cardinality]}]
                               (if (not-empty to-label)
                                 (assoc fields (keyword (normalize-name to-label))
                                        {:type (case cardinality
                                                 ("o2m" "m2m") (list 'list (csk/->PascalCaseKeyword (str (:name to) " input")))
                                                 (csk/->PascalCaseKeyword (str (:name to) " input")))})
                                 fields))
                             input
                             relations))}
              ;; search/filter fields operator
              s {:fields (as->
                           {:_and {:type (list 'list s)}
                            :_or {:type (list 'list s)}
                            :_not {:type (list 'list s)}}
                           operator
                           ;;
                           (reduce
                             (fn [args {:keys [from-label to-label]}]
                               (cond-> args
                                 (not-empty to-label)
                                 (assoc (keyword (normalize-name to-label)) {:type :is_null_enum})
                                 ;;
                                 (not-empty from-label)
                                 (assoc (keyword (normalize-name from-label)) {:type :is_null_enum})))
                             operator
                             recursions)
                           ;;
                           (reduce
                             (fn [fields {aname :name
                                          atype :type
                                          active :active
                                          :as attribute}]
                               (cond
                                 ;; Skip inactive and ignored types
                                 (or (not active) (ignored-field-type? atype))
                                 fields
                                 ;; Handle reference attributes with their entity's search operator
                                 (reference-attribute? attribute)
                                 (assoc fields
                                   (keyword (normalize-name aname))
                                   {:type (entity->search-operator (referenced-entity attribute))})
                                 ;; Regular scalar attributes
                                 :else
                                 (assoc
                                   fields
                                   (keyword (normalize-name aname))
                                   (attribute-type->operator entity attribute))))
                             operator
                             ;; Concat audit search operators from protocol
                             (concat
                               attributes'
                               (enhance/get-search-operators *db* entity))))}
              ;; _order_by operator
              o {:fields (reduce
                           (fn [fields {:keys [to to-label cardinality]}]
                             (case cardinality
                               ("m2o" "o2o")
                               (if (not-empty to-label)
                                 (assoc
                                   fields
                                   (keyword (normalize-name to-label))
                                   {:type (entity->order-by-operator to)})
                                 fields)
                               fields))
                           (reduce
                             (fn [fields {aname :name
                                          atype :type
                                          active :active
                                          :as attribute}]
                               (if-not (or active (= aname (id/field)))
                                 fields
                                 (if (#{"json" "encrypted" "hashed"} atype) fields
                                     (if (reference-attribute? attribute)
                                       (assoc
                                         fields
                                         (keyword (normalize-name aname))
                                         {:type (entity->order-by-operator
                                                  (referenced-entity attribute))})
                                       (assoc
                                         fields
                                         (keyword (normalize-name aname))
                                         {:type :order_by_enum})))))
                             nil
                             attributes')
                           ;; Concat audit order-by operators from protocol
                           (concat
                             relations
                             (enhance/get-order-operators *db* entity)))}
              ;;
              d {:fields (reduce
                           (fn [fields {:keys [to to-label cardinality]}]
                             (case cardinality
                               ;;
                               ("m2o" "o2o")
                               (if (not-empty to-label)
                                 (assoc
                                   fields
                                   (keyword (normalize-name to-label))
                                   {:type (entity->distinct-on-operator to)})
                                 fields)
                               ;; Default
                               fields))
                           ;;
                           (reduce
                             (fn [fields {aname :name
                                          :as attribute}]
                               (assoc fields
                                 (keyword (normalize-name aname))
                                 {:type (entity->distinct-on-operator
                                          (referenced-entity attribute))}))
                             {:attributes
                              {:type (list 'list (entity->attributes-enum entity))}}
                             (filter reference-attribute? attributes))
                           ;; 
                           ;; Concat audit distinct operators from protocol
                           ;; (uses same operator list as order-by)
                           (concat relations (enhance/get-order-operators *db* entity)))})))
        {:UUIDQueryOperator
         {:fields {:_eq {:type 'UUID}
                   :_neq {:type 'UUID}
                   :_in {:type (list 'list 'UUID)}
                   :_not_in {:type (list 'list 'UUID)}}}
         :BooleanQueryOperator
         {:fields {:_boolean {:type 'BooleanCondition}}}
         :StringQueryOperator
         {:fields
          (zipmap
            [:_neq :_eq :_like :_ilike :_in :_not_in]
            (concat
              (repeat 4 {:type 'String})
              (repeat 2 {:type (list 'list 'String)})))}
         :IntegerQueryOperator
         {:fields
          (zipmap
            [:_gt :_lt :_eq :_neq :_ge :_le :_in :_not_in]
            (concat
              (repeat 6 {:type 'Int})
              (repeat 6 {:type (list 'list 'Int)})))}
         :FloatQueryOperator
         {:fields
          (zipmap
            [:_gt :_lt :_eq :_neq :_ge :_le :_in :_not_in]
            (concat
              (repeat 6 {:type 'Float})
              (repeat 6 {:type (list 'list 'Float)})))}

         :TimestampQueryOperator
         {:fields
          (zipmap
            [:_gt :_lt :_neq :_eq :_ge :_le]
            (repeat {:type 'Timestamp}))}}
        entities))))

;;

(defn log-query
  [context]
  (log/debugf
    "Processing query for user %s[%d]:\n%s"
    (:username context)
    (:user context)
    (parser/summarize-query (:com.walmartlabs.lacinia.constants/parsed-query context))))

(defn generate-lacinia-queries [model]
  (let [entities (core/get-entities model)
        user (core/get-entity model (id/entity :iam/user))
        group (core/get-entity model (id/entity :iam/user-group))
        role (core/get-entity model (id/entity :iam/user-role))
        user-operator (entity->search-operator user)
        group-operator (entity->search-operator group)
        role-operator (entity->search-operator role)]
    (letfn [(reference-operator [{atype :type}]
              (case atype
                "user" user-operator
                "group" group-operator
                "role" role-operator))
            (attribute->type [entity attribute]
              (if (reference-attribute? attribute)
                {:type (reference-operator attribute)}
                (attribute-type->operator entity attribute)))]
      (reduce
        (fn [queries {ename :name
                      as :attributes
                      {{uniques :unique} :constraints} :configuration
                      :as entity}]
          (let [relations (core/focus-entity-relations model entity)
                recursions (filter #(= "tree" (:cardinality %)) relations)
                allowed-uniques? (set (map id/extract as))
                uniques (keep
                          (fn [constraints]
                            (when-some [real-ones (filter allowed-uniques? constraints)]
                              (vec real-ones)))
                          uniques)
                entity-id (id/extract entity)
                get-args (reduce
                           (fn [args ids]
                             (reduce
                               (fn [args id]
                                 (let [{aname :name
                                        :as attribute} (core/get-attribute entity id)]
                                   (assoc args (keyword (normalize-name aname))
                                          {:type (cond
                                                   (scalar-attribute? attribute)
                                                   (attribute-type->scalar entity attribute)
                                                   ;;
                                                   (reference-attribute? attribute)
                                                   (reference-operator attribute))})))
                               args
                               ids))
                           {(id/key) {:type (id-scalar-type)}}
                           uniques)
                search-arguments (cond->
                                 ;; Concat audit query args from protocol
                                   (concat as
                                           (enhance/get-query-args *db* entity))
                                   (not-empty recursions)
                                   (as-> args
                                         (reduce
                                           (fn [args {:keys [from-label to-label]}]
                                             (cond-> args
                                               (not-empty to-label)
                                               (conj
                                                 {:name to-label
                                                  :type :is_null_enum
                                                  :active true})
                                               (not-empty from-label)
                                               (conj
                                                 {:name from-label
                                                  :type :is_null_enum
                                                  :active true})))
                                           args
                                           recursions)))]
            (cond->
              (assoc queries
                     ;; GETTER
                (csk/->camelCaseKeyword (str "get " ename))
                {:type (entity->gql-object ename)
                 :args get-args
                 :resolve
                 (fn getter [context data _]
                   (try
                     (get-entity *db* (id/extract entity) data (executor/selections-tree context))
                     (catch Throwable e
                       (log/error e "Couldn't resolve SYNC")
                       (throw e))))}
                     ;; SEARCH
                (csk/->camelCaseKeyword (str "search " ename))
                (let [args (reduce
                             (fn [r {atype :type
                                     aname :name
                                     :as attribute}]
                               (if (ignored-field-type? atype) r
                                   (assoc
                                     r
                                     (keyword (normalize-name aname))
                                     (attribute->type entity attribute))))
                             {:_where {:type (entity->search-operator entity)}
                              :_limit {:type 'Int}
                              :_offset {:type 'Int}
                              :_distinct {:type (entity->distinct-on-operator entity)}
                              :_order_by {:type (entity->order-by-operator entity)}}
                             search-arguments)]
                  (cond->
                    {:type (list 'list (entity->gql-object ename))
                     :resolve
                     (fn search [context data _]
                       (try
                         (log-query context)
                         (lacinia.resolve/resolve-as
                           (search-entity *db* entity-id data (executor/selections-tree context)))
                         (catch Throwable e
                           (log/error e "Couldn't search dataset")
                           (throw e))))}
                    args (assoc :args args)))
                     ;; AGGREGATE
                (csk/->camelCaseKeyword (str "aggregate " ename))
                (let [args (reduce
                             (fn [r {atype :type
                                     aname :name
                                     :as attribute}]
                               (if (ignored-field-type? atype) r
                                   (assoc r (keyword (normalize-name aname))
                                          (attribute->type entity attribute))))
                             {:_where {:type (entity->search-operator entity)}
                              (id/key) {:type (id-operator-type)}}
                             search-arguments)]
                  (cond->
                    {:type (entity->agg-object entity)
                     :resolve
                     (fn aggregate [context data _]
                       (try
                         (log-query context)
                         (let [selection (executor/selections-tree context)]
                           (log/debugf
                             "Aggregating entity\n%s"
                             {:entity ename
                              :data data
                              :selection selection})
                           (aggregate-entity *db* entity-id data selection))
                         (catch Throwable e
                           (log/error e "Couldn't resolve AGGREGATE")
                           (throw e))))}
                    args (assoc :args args))))
              ;; Add recursive getters
              (not-empty recursions)
              (as-> qs
                    (reduce
                      (fn [qs' {l :to-label}]
                        (assoc qs'
                      ;;
                          (csk/->camelCaseKeyword (str "get " ename " tree" " by" " " l))
                          {:type (list 'list (entity->gql-object ename))
                           :args get-args
                           :resolve
                           (fn tree-getter [context data _]
                             (log/debugf
                               "Resolving recursive structure\n%s"
                               {:entity ename
                                :relation l
                                :data data})
                             (try
                               (log-query context)
                               (get-entity-tree
                                 *db*
                                 entity-id
                                 (id/extract data)
                                 (keyword l)
                                 (executor/selections-tree context))
                               (catch Throwable e
                                 (log/error e "Couldn't resolve GET TREE")
                                 (throw e))))}
                      ;;
                          (csk/->camelCaseKeyword (str "search " ename " tree by " l))
                          (let [args (reduce
                                       (fn [r {atype :type
                                               aname :name
                                               :as attribute}]
                                         (if (ignored-field-type? atype) r
                                             (assoc
                                               r
                                               (keyword (normalize-name aname))
                                               (attribute->type entity attribute))))
                                       {:_where {:type (entity->search-operator entity)}
                                        :_limit {:type 'Int}
                                        :_offset {:type 'Int}
                                        :_order_by {:type (entity->order-by-operator entity)}}
                                       search-arguments)]
                            (cond->
                              {:type (list 'list (entity->gql-object ename))
                               :resolve
                               (fn tree-search [context data _]
                                 (try
                                   (log-query context)
                                   (let [selection (executor/selections-tree context)]
                                     (log/debugf
                                       "Searching entity tree\n%s"
                                       {:name ename
                                        :data data
                                        :selection selection})
                                     (search-entity-tree *db* entity-id (keyword (normalize-name l)) data selection))
                                   (catch Throwable e
                                     (log/error e "Couldn't resolve SEARCH TREE")
                                     (throw e))))}
                              args (assoc :args args)))
                      ;;
                          (csk/->camelCaseKeyword (str "aggregate " ename " tree by " l))
                          (let [args (reduce
                                       (fn [r {atype :type
                                               aname :name
                                               :as attribute}]
                                         (if (ignored-field-type? atype) r
                                             (assoc
                                               r
                                               (keyword (normalize-name aname))
                                               (attribute->type entity attribute))))
                                       {:_where {:type (entity->search-operator entity)}
                                        (id/key) {:type (id-operator-type)}}
                                       search-arguments)]
                            (cond->
                              {:type (entity->agg-object entity)
                               :resolve
                               (fn tree-aggregator [context data _]
                                 (try
                                   (log-query context)
                                   (let [selection (executor/selections-tree context)]
                                     (log/debugf
                                       "Aggregating entity tree\n%s"
                                       {:entity ename
                                        :data data
                                        :selection selection})
                                     (aggregate-entity-tree *db* entity-id (keyword (normalize-name l)) data selection))
                                   (catch Throwable e
                                     (log/error e "Couldn't resolve AGGREGATE TREE")
                                     (throw e))))}
                              args (assoc :args args)))))
                      qs
                      recursions)))))
        {}
        entities))))

(defn sync-mutation
  [{entity :eywa/entity
    :as context} data _]
  (log-query context)
  (let [entity-id (id/extract entity)
        result (sync-entity *db* entity-id (val (first data)))
        row-id (id/extract result)
        selection (executor/selections-tree context)
        ; _ (log/infof
        ;     :message "Getting entity"
        ;     :entity ename :row row :selection selection)
        value (get-entity *db* entity-id {(id/key) row-id} selection)]
    value))

(defn sync-list-mutation
  [{entity :eywa/entity
    :as context}
   data
   _]
  (log-query context)
  (let [entity-id (id/extract entity)
        rows (sync-entity *db* entity-id (val (first data)))
        rows' (mapv id/extract rows)
        selection (executor/selections-tree context)
        value (search-entity
                *db* entity-id
                {:_where {(id/key) {:_in rows'}}}
                selection)]
    value))

(defn stack-mutation
  [{:as context
    entity :eywa/entity}
   data
   _]
  (log-query context)
  (let [entity-id (id/extract entity)
        result (stack-entity *db* entity-id (val (first data)))
        row-id (id/extract result)
        selection (executor/selections-tree context)
        value (get-entity *db* entity-id {(id/key) row-id} selection)]
    value))

(defn stack-list-mutation
  [{entity :eywa/entity
    :as context}
   data
   _]
  (log-query context)
  (let [entity-id (id/extract entity)
        rows (stack-entity *db* entity-id (val (first data)))
        rows' (mapv id/extract rows)
        selection (executor/selections-tree context)
        value (search-entity
                *db* entity-id
                {:_where {(id/key) {:_in rows'}}}
                selection)]
    value))

(defn slice-mutation
  [{:as context
    entity :eywa/entity}
   data
   _]
  (let [entity-id (id/extract entity)
        args data
        selection (executor/selections-tree context)]
    ; (log/trace
    ;   :message "Slicing entity" 
    ;   :euuid euuid :args args :selection selection
    ;   (with-out-str (pprint args)) 
    ;   (with-out-str (pprint selection)))
    (slice-entity *db* entity-id args selection)))

(defn delete-mutation
  [{entity :eywa/entity}
   data
   _]
  (delete-entity *db* (id/extract entity) data))

(defn purge-mutation
  [{:as context
    entity :eywa/entity}
   data
   _]
  (log-query context)
  (let [args data
        selection (executor/selections-tree context)]
    (purge-entity *db* (id/extract entity) args selection)))

(defn generate-lacinia-mutations [model]
  (let [entities (core/get-entities model)]
    (comment
      (def entity (core/get-entity model #uuid "ec30827b-c1b1-4d1f-889b-77f5bf4b35bf"))
      (def ename (:name entity))
      (core/get-entity-relations model entity))
    (reduce
      (fn [mutations {ename :name
                      :as entity}]
        (let [t (entity->gql-input-object ename)
              relations (core/focus-entity-relations model entity)
              to-relations (filter #(not-empty (:to-label %)) relations)]
          (cond->
            (assoc mutations
                   ;;
              (csk/->camelCaseKeyword (str "sync " ename))
              {:type (entity->gql-object ename)
               :args {:data {:type (list 'non-null t)}}
               :resolve
               (fn sync [context data value]
                 (try
                   (sync-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:data id-key])]
                       (log/error e "Couldn't resolve SYNC")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "sync " ename))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))}
                   ;; SYNC LIST
              (csk/->camelCaseKeyword (str "sync " ename " List"))
              {:type (list 'list (entity->gql-object ename))
               :args {:data {:type (list 'list t)}}
               :resolve
               (fn syncList [context data value]
                 (try
                   (sync-list-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:data 0 id-key])]
                       (log/error e "Couldn't resolve SYNC list")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "sync " ename " List"))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))}
                   ;;
              (csk/->camelCaseKeyword (str "stack " ename " List"))
              {:type (list 'list (entity->gql-object ename))
               :args {:data {:type (list 'list t)}}
               :resolve
               (fn stackList [context data value]
                 (try
                   (stack-list-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:data 0 id-key])]
                       (log/error e "Couldn't resolve STACK list")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "stack " ename " List"))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))}
                   ;;
              (csk/->camelCaseKeyword (str "stack" ename))
              {:type (entity->gql-object ename)
               :args {:data {:type (list 'non-null t)}}
               :resolve
               (fn stack [context data value]
                 (try
                   (stack-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:data id-key])]
                       (log/error e "Couldn't resolve STACK")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "stack " ename))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))}
                   ;;
              (csk/->camelCaseKeyword (str "delete " ename))
              (let [allowed? (set (map id/extract (:attributes entity)))
                    uniques (keep
                              (fn [constraints]
                                (when-some [real-constraints (not-empty (filter allowed? constraints))]
                                  (vec real-constraints)))
                              (-> entity :configuration :constraints :unique))
                    args (reduce
                           (fn [args ids]
                             (reduce
                               (fn [args id]
                                 (let [{aname :name
                                        :as attribute} (core/get-attribute entity id)]
                                   (assoc args (keyword (normalize-name aname))
                                          {:type (attribute-type->scalar entity attribute)})))
                               args
                               ids))
                           {(id/key) {:type (id-scalar-type)}}
                           uniques)]
                (comment
                  (csk/->camelCaseKeyword (str "delete " "OAuth Scope")))
                     ; (log/debugf "Adding delete method for %s\n%s" ename args)
                {:type 'Boolean
                 :args args
                 :resolve (fn delete [context data value]
                            (try
                              (delete-mutation (assoc context :eywa/entity entity) data value)
                              (catch Throwable e
                                (let [id-key (id/key)
                                      entity-id (get data id-key)]
                                  (log/error e "Couldn't resolve DELETE")
                                  (lacinia.resolve/resolve-as nil
                                                              {:message (.getMessage e)
                                                               :extensions (cond-> {:field-name (csk/->camelCaseString (str "delete " ename))
                                                                                    :entity ename
                                                                                    :arguments data}
                                                                             entity-id (assoc (keyword (name id-key)) entity-id))})))))})
                   ;;
              (csk/->camelCaseKeyword (str "purge " ename))
              {:type (list 'list (entity->gql-object ename))
               :args {:_where {:type (entity->search-operator entity)}}
               :resolve
               (fn purge [context data value]
                 (try
                   (purge-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:_where id-key :_eq])]
                       (log/error e "Couldn't resolve purge")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "purge " ename))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))})
            ;;
            (not-empty to-relations)
            (assoc
              ;; Slicing
              (csk/->camelCaseKeyword (str "slice " ename))
              {:type (entity->slice-object entity)
               :args {:_where {:type (entity->search-operator entity)}}
               :resolve
               (fn slice [context data value]
                 (try
                   (slice-mutation (assoc context :eywa/entity entity) data value)
                   (catch Throwable e
                     (let [id-key (id/key)
                           entity-id (get-in data [:_where id-key :_eq])]
                       (log/error e "Couldn't resolve SLICE")
                       (lacinia.resolve/resolve-as nil
                                                   {:message (.getMessage e)
                                                    :extensions (cond-> {:field-name (csk/->camelCaseString (str "slice " ename))
                                                                         :entity ename
                                                                         :arguments data}
                                                                  entity-id (assoc (keyword (name id-key)) entity-id))})))))}))))
      {}
      entities)))

(comment
  (def model (dataset/deployed-model))
  (def entity (core/get-entity model #uuid "ec30827b-c1b1-4d1f-889b-77f5bf4b35bf"))
  (core/get-entity model #uuid "c3835dcb-b8d7-40b7-be1c-97b7f50225d0")
  (core/focus-entity-relations model entity)
  (do (generate-lacinia-objects model) nil))

(defn filter-active-model
  "Returns a model with only active entities and relations"
  [model]
  (let [active-entities (into {}
                              (filter (fn [[_uuid entity]] (:active entity))
                                      (:entities model)))
        active-relations (into {}
                               (filter (fn [[_uuid relation]] (:active relation))
                                       (:relations model)))]
    (assoc model
      :entities active-entities
      :relations active-relations)))

(defn generate-lacinia-schema
  "Generates a complete Lacinia schema from the dataset model.

  Uses scalars from synthigy.lacinia for consistency across all shards."
  ([] (generate-lacinia-schema (dataset/deployed-model)))
  ([model]
   ;; Filter to only active entities and relations
   (let [active-model (filter-active-model model)
         service-definition {:enums (generate-lacinia-enums active-model)
                             :objects (assoc
                                        (generate-lacinia-objects active-model)
                                        :Mutation {:fields (generate-lacinia-mutations active-model)}
                                        :Query {:fields (generate-lacinia-queries active-model)})
                             :input-objects (generate-lacinia-input-objects active-model)}
         schema service-definition]
     (merge schema {:scalars synthigy.lacinia/scalars}))))
